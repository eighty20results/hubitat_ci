plugins {
    id 'groovy'
    id 'maven-publish'
}

// Allow overriding version via -Pversion or VERSION env; fallback to default
def versionProperty = project.findProperty('version')
version = (versionProperty != null && versionProperty != 'unspecified') ? versionProperty : (System.getenv('VERSION') ?: '0.26.3')

repositories {
    mavenCentral()
}

tasks.withType(GroovyCompile) {
    groovyOptions.configurationScript = file('groovy-config.groovy')
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
        showStandardStreams = false
    }
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    archiveClassifier = 'groovydoc'
    from groovydoc.destinationDir
}

// Publishing is always enabled; credentials must be provided via env vars when used
publishing {
    publications {
        maven(MavenPublication){
            groupId = System.getenv("MAVEN_GROUP_ID") ?: 'me.eighty20results.hubitat_ci'
            artifactId = System.getenv("MAVEN_ARTIFACT_ID") ?: 'hubitat_ci'
            version = version

            from components.java

            artifact sourcesJar
            artifact groovydocJar
        }
    }

    repositories{
        maven {
            url = System.getenv("MAVEN_ENDPOINT") ?: "https://maven.pkg.github.com/eighty20results/hubitat_ci"
            credentials {
                username = System.getenv("REPO_USERNAME") ?: ""
                password = System.getenv("REPO_TOKEN") ?: ""
            }
        }
    }
}

dependencies {
    implementation 'org.apache.commons:commons-io:1.3.2'
    implementation 'org.codehaus.groovy:groovy:3.0.21'
    implementation 'org.codehaus.groovy:groovy-dateutil:3.0.21'
    implementation 'org.codehaus.groovy:groovy-json:3.0.21'
    implementation 'org.codehaus.groovy:groovy-xml:3.0.21'
    implementation 'org.codehaus.groovy:groovy-sql:3.0.21'
    implementation 'org.codehaus.groovy:groovy-swing:3.0.21'
    implementation 'org.ow2.asm:asm:9.5'
    implementation 'org.quartz-scheduler:quartz:2.3.0'

    implementation 'su.litvak.chromecast:api-v2:0.11.1'

    implementation 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'

    implementation "org.spockframework:spock-core:2.3-groovy-3.0"
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.junit.vintage:junit-vintage-engine:5.9.2'
    testImplementation group: 'cglib', name: 'cglib-nodep', version: '3.3.0'
}
