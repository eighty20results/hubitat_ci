plugins {
    id 'groovy'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    archiveClassifier = 'groovydoc'
    from groovydoc.destinationDir
}

if (System.getenv("AZURE_ARTIFACTS_ENV_ACCESS_TOKEN") || project.hasProperty('azureArtifactsGradleAccessToken'))
{
publishing {
    publications {
        maven(MavenPublication){
            groupId = System.getenv("MAVEN_GROUP_ID") ?: 'me.eighty20results.hubitat_ci'
            artifactId = System.getenv("MAVEN_ARTIFACT_ID") ?: 'hubitat_ci'
            version = '0.26.1'

            from components.java

            artifact sourcesJar
            artifact groovydocJar
        }
    }

        repositories{
            maven {
                url = System.getenv("MAVEN_ENDPOINT") ?: "https://maven.pkg.github.com/eighty20results/hubitat_ci"
                credentials {
                    username = System.getenv("REPO_USERNAME") ?: ""
                    password = System.getenv("REPO_TOKEN") ?: ""
                }
            }
        }
    }
}

dependencies {
    implementation 'org.apache.commons:commons-io:1.3.2'
    implementation 'org.codehaus.groovy:groovy-all:2.5.4'
    implementation 'org.codehaus.groovy:groovy-dateutil:2.5.4'
    implementation 'org.ow2.asm:asm:7.0'
    implementation 'org.quartz-scheduler:quartz:2.3.0'

    implementation 'su.litvak.chromecast:api-v2:0.11.1'

    implementation 'org.codehaus.groovy.modules.http-builder:http-builder:0.5.0'

    implementation "org.spockframework:spock-core:1.2-groovy-2.5"
    testImplementation 'junit:junit:4.12'
    testImplementation group: 'cglib', name: 'cglib-nodep', version: '3.3.0'
}
